group 'com.tiza'
version '1.0'

apply plugin: 'java'

sourceCompatibility = 1.7

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencies {
    compile project(':tstar-plugin')
    compile fileTree(dir: project(':tstar-plugin').projectDir.path + "/libs/tstar-rp", include: "**/*.jar")

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

// 设置编码
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

// 拷贝文件(排除项目自带jar)
task copyJar(type: Copy) {
    from configurations.runtime
    into "$buildDir/dist"

    exclude {
        !it.file.name.contains('tstar-plugin') &&
                it.file.path.contains('libs\\tstar')
    }
}

// 拷贝配置文件
task copyConf(type: Copy) {
    from "$projectDir/src/main/conf"
    into "$buildDir/libs/conf"
}


jar {
    archiveName = 'trash_jt808.jar'
    version = ''

    from {
        File srcDir = file("$buildDir/dist")
        // 使用闭合创建一个文件集合
        FileCollection collection = files { srcDir.listFiles() }
        collection.collect { it.isDirectory() ? it : zipTree(it) }
    }
}
jar.dependsOn(copyJar)

task zip(type: Zip, dependsOn: [jar, copyConf]) {
    version = ''
    from 'build/libs'
}